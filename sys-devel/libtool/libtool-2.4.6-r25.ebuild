# Copyright 1999-2021 Gentoo Authors
# Distributed under the terms of the GNU General Public License v2

EAPI=7

LIBTOOLIZE="true" #225559
WANT_LIBTOOL="none"
inherit autotools prefix rhel8

if [[ ${PV} != *8888 ]]; then
	KEYWORDS="~alpha amd64 arm arm64 hppa ~ia64 ~m68k ~mips ppc ppc64 ~riscv ~s390 sparc x86 ~x64-cygwin ~amd64-linux ~x86-linux ~ppc-macos ~x64-macos ~sparc-solaris ~sparc64-solaris ~x64-solaris ~x86-solaris ~x86-winnt"
fi

DESCRIPTION="A shared library tool for developers"
HOMEPAGE="https://www.gnu.org/software/libtool/"

LICENSE="GPL-2"
SLOT="2"
IUSE="vanilla"

# Pull in libltdl directly until we convert packages to the new dep.
RDEPEND="
	sys-devel/gnuconfig
	>=sys-devel/autoconf-2.69:*
	>=sys-devel/automake-1.13:*
	dev-libs/libltdl:0"
DEPEND="${RDEPEND}"
[[ ${PV} == *8888 ]] && BDEPEND="sys-apps/help2man"

src_prepare() {
	if [[ ${PV} == *8888 ]] ; then
		eapply "${FILESDIR}"/${PN}-2.4.6-pthread.patch #650876
		./bootstrap || die
	else
		PATCHES+=(
			"${FILESDIR}"/${PN}-2.4.6-pthread_bootstrapped.patch #650876
		)
	fi

	# WARNING: File build-aux/ltmain.sh is read-only; trying to patch anyway
	chmod +w build-aux/ltmain.sh || die

	if use vanilla ; then
		eapply_user
		return 0
	else
		default
	fi

	if use prefix ; then
		# seems that libtool has to know about EPREFIX a little bit
		# better, since it fails to find prefix paths to search libs
		# from, resulting in some packages building static only, since
		# libtool is fooled into thinking that libraries are unavailable
		# (argh...). This could also be fixed by making the gcc wrapper
		# return the correct result for -print-search-dirs (doesn't
		# include prefix dirs ...).
		eapply "${FILESDIR}"/${PN}-2.2.10-eprefix.patch
		eprefixify m4/libtool.m4
	fi

	pushd libltdl >/dev/null || die
	AT_NOELIBTOOLIZE=yes eautoreconf
	popd >/dev/null || die
	AT_NOELIBTOOLIZE=yes eautoreconf

	# Make sure timestamps don't trigger a rebuild of man pages. #556512
	if [[ ${PV} != *8888 ]] ; then
		touch doc/*.1 || die
		export HELP2MAN=false
	fi
}

src_configure() {
	# the libtool script uses bash code in it and at configure time, tries
	# to find a bash shell.  if /bin/sh is bash, it uses that.  this can
	# cause problems for people who switch /bin/sh on the fly to other
	# shells, so just force libtool to use /bin/bash all the time.
	export CONFIG_SHELL="$(type -P bash)"

	# Do not bother hardcoding the full path to sed.  Just rely on $PATH. #574550
	export ac_cv_path_SED="$(basename "$(type -P sed)")"

	[[ ${CHOST} == *-darwin* ]] && local myconf="--program-prefix=g"
	ECONF_SOURCE=${S} econf ${myconf} --disable-ltdl-install
}

src_compile() {
	emake CUSTOM_LTDL_CFLAGS="-specs=/usr/lib/rpm/redhat/redhat-hardened-cc1" \
	CUSTOM_LTDL_LDFLAGS="-Wl,-z,now -specs=/usr/lib/rpm/redhat/redhat-hardened-ld"
}

src_test() {
	emake check VERBOSE=yes || { cat testsuite.log ; false ; }
}

src_install() {
	default
	# info's TOP dir (by default owned by info)
	rm -f ${ED}/usr/share/info/dir
	# *.la *.a files generated by libtool shouldn't be distributed (and the
	# `./configure --disable-static' breaks testsuite)
	#rm -f ${ED}/usr/lib64/libltdl.{a,la}

	local x
	while read -d $'\0' -r x ; do
		ln -sf "${EPREFIX}"/usr/share/gnuconfig/${x##*/} "${x}" || die
	done < <(find "${ED}" '(' -name config.guess -o -name config.sub ')' -print0)
}
